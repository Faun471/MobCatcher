plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'me.faun'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'jitpack-io'
        url = 'https://jitpack.io'
    }
    maven {
        url = 'https://repo.triumphteam.dev/snapshots/'
    }
    maven {
        url = 'https://hub.jeff-media.com/nexus/repository/jeff-media-public/'
    }
    maven {
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    implementation 'com.jeff_media:MorePersistentDataTypes:2.3.1'
    implementation 'net.kyori:adventure-api:4.10.1'
    implementation 'dev.triumphteam:triumph-cmd-bukkit:2.0.0-SNAPSHOT'
    implementation 'me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT'
    implementation 'ch.jalu:configme:1.3.0'
    implementation 'de.tr7zw:item-nbt-api-plugin:2.9.2'
}

def targetJavaVersion = 16
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier = null
    relocate 'dev.triumphteam.cmd', 'me.faun.catchmob.cmd'
    relocate 'me.mattstudios.msg', 'me.faun.catchmob.mfmsg'
    relocate 'com.jeff_media.morepersistentdatatypes', 'me.faun.morepersistentdatatypes'
    relocate 'de.tr7zw.changeme.nbtapi', 'me.faun.nbtapi'
}